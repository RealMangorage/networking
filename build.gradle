plugins {
    id 'java'
}

class Dependency {
    String configuration
    Object dep
    List<Object> projects

    Dependency(String configuration, Object dep, List<Object> projects) {
        this.configuration = configuration
        this.dep = dep
        this.projects = projects
    }
}

ext {
    deps = []
}

void addDependency(String configuration, Object dep, List<Object> projects) {
    deps.add(new Dependency(configuration, dep, projects))
}

// Add dependencies for projects here!
// Make sure to set maven repos in the subprojects branch
addDependency('implementation', project('common'), ['server', 'client'])
addDependency('implementation', 'com.google.code.gson:gson:2.8.8', [])
addDependency('implementation', 'com.github.Querz:NBT:6.1', [])
addDependency('implementation', 'io.netty:netty-all:4.1.100.Final', [])

addDependency('testImplementation', project.dependencies.platform('org.junit:junit-bom:5.10.0'), [])
addDependency('testImplementation', 'org.junit.jupiter:junit-jupiter', [])

subprojects { subproject ->
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url = 'https://jitpack.io/'
        }
    }

    dependencies {
        deps.each { dep ->
            if (dep.projects.contains(subproject.name) || dep.projects.isEmpty()) {
                subproject.dependencies.add(dep.configuration, dep.dep)
                project.logger.lifecycle("Added ${dep.dep} dependency to ${subproject.name} project under '${dep.configuration}' configuration")
            }
        }
    }
}